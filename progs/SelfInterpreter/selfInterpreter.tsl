
involution selfInterpreter (Program.Input)
    ('program.(Main . (Invols . Procs))) <- Program
    ('invol. (Id. (P. Body))) <- Main
    Table <- call createTable (Invols.Procs) 
    Env <- call deconstruct (('nil.Table).(P.Input))
    (Env.Body) <- involute interpretInvolutionBody (Env.Body)










// ------------------------------- Interpreters ------------------------------------------------------------------------
involution interpretInvolutionBody (Env.Body)
    (Init.Last) <- call unsnoc Body
    (Env.Init) <- call interpretStatements Init
    (Env.Last) <- involute interpretSymmetricStatement (Env.Last)


procedure interpretStatements (Env.Statements)
    if Statements = 'nil
    then skip
    else
        (Statement.Statements) <- Statements
        (Env.Statement) <- call interpretStatement (Env.Statement)
        (Env.Statements) <- call interpretStatements (Env.Statements)
        Statements <- (Statement.Statements)
    fi Statements = 'nil
    return (Env.Statements)

procedure interpretStatement (Env.Statement)
    (Stype.Statement) <- Statement
    if Stype = 'assign then
        (Op.(Var.E)) <- Statement
        (Env.(E.V2)) <- call interpretExpression (Env.E)
        (Env.(Var.V1)) <- uncall insertVar (Env.Var)
        (Op.(V.V2)) <- call applyRevOp (Op.(V1.V2))
        (Env.Var) <- call insertVar (Env.(Var.V))
        (Env.E) <- uncall interpretExpression (Env.(E.V2))
        Statement <- (Op.(Var.E))
    else if Stype = 'loopS then
        (E1.(S1.(S2.E2))) <- Statement
        (Env.(E1.B1)) <- call interpretExpression (Env.E1)
        from B1
        do
            (Env.E1) <- uncall interpretExpression (Env.(E1.B1))
            (Env.S1) <- call interpretStatements (Env.S1)
            (Env.(E2.B2)) <- call interpretExpression (Env.E2)
        loop
            (Env.E2) <- uncall interpretExpression (Env.(E2.B2))
            (Env.S2) <- call interpretStatements (Env.S2)
            (Env.(E1.B1)) <- call interpretExpression (Env.E1)
        until B2
        (Env.E2) <- uncall interpretExpression (Env.(E2.B2))
        Statement <- (E1.(S1.(S2.E2)))

    else if Stype = 'conditional then
        (E1.(S1.(S2.E2))) <- Statement
        (Env.(E1.B1)) <- call interpretExpression (Env.E1)
        if B1 then
            (Env.E1) <- uncall interpretExpression (Env.(E1.B1))
            (Env.S1) <- call interpretStatements (Env.S1)
            (Env.(E2.B2)) <- call interpretExpression (Env.E2)
        else
            (Env.E1) <- uncall interpretExpression (Env.(E1.B1))
            (Env.S2) <- call interpretStatements (Env.S2)
            (Env.(E2.B2)) <- call interpretExpression (Env.E2)
        fi B2
        (Env.E2) <- uncall interpretExpression (Env.(E2.B2))
        Statement <- (E1.(S1.(S2.E2)))

    else if Stype = 'replacement then
        (P1.P2) <- Statement
        (Env.(P1.V)) <- uncall deconstruct (Env.P1)
        (Env.P2) <- call deconstruct (Env.(P2.V))
        Statement <- (P1.P2)

    else if Stype = 'skipS then
        skip
    else skip //should not reach here!!
    fi Stype = 'skipS
    fi Stype = 'replacement
    fi Stype = 'conditional
    fi Stype = 'loopS
    fi Stype = 'assign
    Statement <- (Stype.Statement)

    return (Env.Statement)


involution interpretSymmetricStatement (Env. Statement)
    Env' ^= Env
    (Env. Statement) <- call interpretStatement (Env. Statement)
    (Env.Env') <- (Env'.Env)

procedure interpretExpression (Env.E)
    (Etype.E) <- E
    if Etype = 'constant then
        V^=E
    else if Etype = 'variable then
        (Env.(E.V)) <- call lookVar (Env.E)
    else if Etype = 'operation then
        (Op.(E1.E2)) <- E
        (Env.(E1.V1)) <- call interpretExpression (Env.E1)
        (Env.(E2.V2)) <- call interpretExpression (Env.E2)
        (Op.(V1.(V2.V))) <- call applyOp (Op.(V1.V2))
        (Env.E1) <- uncall interpretExpression (Env.(E1.V1)) 
        (Env.E2) <- uncall interpretExpression (Env.(E2.V2))
        E <- (Op.(E1.E2))
    else skip // Should never reach here
    fi Etype = 'operation
    fi Etype = 'variable
    fi Etype = 'constant
    return (Env.(E.V))







// -------------------------------- Auxillary procedures -----------------------------------------------------------------

procedure deconstruct (Env.(Pattern.Value))
    (Ptype.Pvalue) <- Pattern
    if Ptype = 'variable then
       (Env.Pvalue) <- call insertVar (Env.(Pvalue.Value))
    else if Ptype = 'constant && Pvalue = Value then skip
    else if Ptype = 'pair then
        (P1.P2) <- Pvalue
        ('pair.(C1.C2)) <- Value
        (Env.P1) <- call deconstruct (Env.(P1.C1))
        (Env.P2) <- call deconstruct (Env.(P2.C2))
        Pvalue <- (P1.P2)
    else if Ptype = 'involuteS then
        (Id.P) <- Pvalue
        (Env.(P.Input)) <- uncall deconstruct (Env.P) 
        (Env.(Id.('invol. (Id'.(P'.Body))))) <- call lookInvol (Env.Id)
        (Env.InvolEnv) <- call createEmptyEnv Env
        (InvolEnv.P') <- call deconstruct (InvolEnv.(P'.Input))
        (InvolEnv.Body) <- involute interpretInvolutionBody (InvolEnv.Body)
        (InvolEnv.(P'.Output)) <- uncall deconstruct (InvolEnv.P')
        Env <- uncall createEmptyEnv (Env.InvolEnv)
        (Env.Id) <- uncall lookInvol (Env.(Id.('invol. (Id'.(P'.Body)))))
        (Env.P) <- call deconstruct (Env.(P.Output))
        Pvalue <- (Id.P)
    else if Ptype = 'callS then
        (Env.Pvalue) <- call evalCall (Env.Pvalue)
    else if Ptype = 'uncallS then
        (Env.Pvalue) <- uncall evalCall (Env.Pvalue)
    else Ptype ^= 'error //should never reach here
    fi Ptype = 'uncallS
    fi Ptype = 'callS
    fi Ptype = 'involuteS
    fi Ptype = 'pair
    fi Ptype = 'constant
    fi Ptype ='variable
    Pattern <- (Ptype.Pvalue)
    return (Env.Pattern)

procedure evalCall (Env.(Id.P))
    (Env.(P.Input)) <- uncall deconstruct (Env.P)
    (Env.(Id.('proc. (Id'.(PIn.(Body.POut)))))) <- call lookProc (Env.Id)
    (Env.ProcEnv) <- call createEmptyEnv Env
    (ProcEnv.PIn) <- call deconstruct (ProcEnv.(PIn.Input))
    (ProcEnv.Body) <- call interpretStatements (ProcEnv.Body)
    (ProcEnv.(POut.Output)) <- uncall deconstruct (ProcEnv.POut)
    Env <- uncall createEmptyEnv (Env.ProcEnv)
    (Env.Id) <- uncall lookProc (Env.(Id.('proc. (Id'.(PIn.(Body.POut))))))
    (Env.P) <- call deconstruct (Env.(P.Output))
    return (Env.(Id.P))


procedure applyOp (Op.(V1.V2))
    if Op = 'Xor then
    else if Op = 'Add then
        V ^= V1 + V2
    else if Op = 'Sub then
        V ^= V1 - V2
    else if Op = 'Mult then
        V ^= V1 * V2
    else if Op = 'Div then
        V ^= V1 / V2
    else if Op = 'Mod then
        V ^= V1 % V2
    else if Op = 'And then
        V ^= V1 && V2
    else if Op = 'Or then
        V ^= V1 || V2
    else if Op = 'Gt then
        V ^= V1 > V2
    else if Op = 'Eq then
        V ^= V1 = V2
    else if Op = 'Lt then
        V ^= V1 < V2
    else if Op = 'GtEq then
        V ^= V1 >= V2
    else if Op = 'LtEq then
        V ^= V1 <= V2
    else if Op = 'Neq then
        V ^= V1 != V2
    else skip // Should never reach here
    fi Op = 'Neq
    fi Op = 'LtEq
    fi Op = 'GtEq
    fi Op = 'Lt
    fi Op = 'Eq
    fi Op = 'Gt
    fi Op = 'Or
    fi Op = 'And
    fi Op = 'Mod
    fi Op = 'Div
    fi Op = 'Mult
    fi Op = 'Sub
    fi Op = 'Add
    fi Op = 'Xor
    return (Op.(V1.(V2.V)))

// ----------------------------------------- Environment auxillary functions -----------------------------------------------
// Environment = (Table.Store)
// Table = (Invols.Procs)
// Store = Map Var Value
// Invols = Map Id involution
// Procs = Map Id procedure

procedure lookVar ((Table.Store).Var)
    (Store.KVPair) <- call look (Store.Var)
    return ((Table.Store).KVPair)

procedure insertVar ((Table.Store).KVPair)
    (Store.KVPair) <- call insert (Store.KVPair)
    return ((Table.Store).KVPair)

procedure lookInvol (((Invols.Procs).Store).Id)
    (Invols.KVPair) <- call look (Invols.Id)
    return (((Invols.Procs).Store).KVPair)

procedure lookProc (((Invols.Procs).Store).Id)
    (Procs.KVPair) <- call look (Procs.Id)
    return (((Invols.Procs).Store).KVPair)

procedure createTable (Invols.Procs)
    InvolTable <- call createInvolTable Invols
    ProcTable <- call createProcTable Procs
    return (InvolTable.ProcTable)

procedure createInvolTable Invols
    if Invols = 'nil then skip
    else
        (Invol.Invols) <- Invols
        ('invol. (Id.Invol)) <- Invol
        InvolTable <- call createInvolTable Invols
        InvolTable <- ((Id.('invol.Invol)).InvolTable)
    fi InvolTable = 'nil
    return InvolTable

procedure createProcTable Procs
    if Procs = 'nil then skip
    else
        (Proc.Procs) <- Procs
        ('proc. (Id.Proc)) <- Proc
        ProcTable <- call createProcTable Procs
        ProcTable <- ((Id.('proc. Proc)).ProcTable)
    fi ProcTable = 'nil
    return ProcTable


procedure createEmptyEnv (Table.Store)
    Table' ^= Table
    return ((Table.Store).(Table'.'nil))





// ----------------------------------------- Map functions ----------------------------------------------------------------

procedure look (Map.Key)
    (Map.(Key.Value)) <- uncall insert (Map.Key)
    Value' ^= Value
    (Map.Key) <- call insert (Map.(Key.Value'))
    return (Map.(Key.Value))

//Assumes key is not already in map
procedure insert (Map. (Key.Value))
    //Maybe check here if key is already in (then value must be nil)
    // Or enforce this invariant elsewhere?
    // For instance if only using insert to modify map, then the invariant holds
    // as we never insert with 'nil as value.
    if Value = 'nil
    then (IsIn.(Map.Key)) <- call isKey (Map.Key)
    else
        if Map = 'nil
        then Key' ^= Key
            Map <- ((Key'.Value).'nil)
            (Map.Length) <- call length Map
        else ((Key'.Value').Map) <- Map
            if Key' < Key then
                (Map.Key) <- call insert (Map. (Key.Value))
            else
                Map <- ((Key'.Value').Map)
                (Key'.Value') <- (Key.Value)
                Key ^= Key'
            fi Key' < Key
            Map <- ((Key'.Value').Map)
            (Map.Length) <- call length Map
        fi Length = '1
        Map <- uncall Length (Map.Length)
        (IsIn.(Map.Key)) <- call isKey (Map.Key)
    fi IsIn = 'nil
    (Map.Key) <- uncall isKey (IsIn.(Map.Key))
    return (Map.Key)


procedure isKey (Map.Key)
    if Map = 'nil
    then skip
    else
        ((Key'.Val').Map) <- Map
        if Key' = Key
        then In ^= 'true
        else (In.(Map.Key)) <- call elem (Map.Key)
        fi Key' = Key
        Map <- ((Key'.Val').Map)
    fi Map = 'nil
    return (In.(Map.Key))

// ------------------------------------- List operations -----------------------------------------------------------------


procedure length List
    (List.C) <- call countRec (List.'0)
    return (List.C)

procedure countRec (List.C)
    if List = 'nil
    then skip
    else C+='1
         (Head.Tail) <- List
         (Tail.C) <- call countRec (Tail.C)
         List <- (Head.Tail)
    fi List = 'nil
    return (List.C)


procedure unsnoc List
    if List = 'nil
    then skip
    else
        (List.C) <- call length List
        if C = '1 then
            (Last.'nil) <- List

        else
            (Head.Tail) <- List
            (Init.Last) <- call unsnoc Tail
            Init <- (Head.Init)
        fi C = '1
        C -= '1
        Init <- uncall length (Init.C)
    fi Last = 'nil && Init = 'nil
    return (Init.Last)

